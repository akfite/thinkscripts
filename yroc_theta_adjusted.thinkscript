# YROC-X% (Yearly Return On Capital, theta-adjusted)
# 
#   The expected annualized return if the option were sold and closed for partial
#   profit repeatedly throughout a year.
#

# define constants
input PROFIT_TARGET = 0.5; # [0-1], the percentage profit at which we will close

input SPREAD_CROSSING_RATIO = 0.1; # assume we can do slightly better than the bid (0=sell to bid)
input FEE_PER_CONTRACT = 0.45; # usually $0.65/contract unless you negotiate it
input PUT_MARGIN_RATIO = 1; # [0-1]; 1 = no margin (value = Max(AVAILABLE_CASH / PUT_NOTIONAL_VALUE, 1))
input TIME_EFFICIENCY = 1; # expected percent of year spent with a contract active

# all further calculations are PER SHARE
def bid_ = bid(); # avoid calling this a ton of times
def spread = ask() - bid_; # option price spread
def strike = GetStrike();
def underlying = close(GetUnderlyingSymbol(), priceType = PriceType.LAST);

def total_premium = bid_ + (SPREAD_CROSSING_RATIO * spread) - (2 * FEE_PER_CONTRACT / 100); # fee to open & close
def intrinsic_premium;
def extrinsic_premium;
def return_on_capital;

# report returns assuming the share price does not move
if (IsPut()) {
    # selling puts
    intrinsic_premium = Max(strike - underlying, 0);
    extrinsic_premium = total_premium - intrinsic_premium;
    return_on_capital = Max(extrinsic_premium / (strike * PUT_MARGIN_RATIO), 0); # clamp to [0, inf]
} else {
    # buying underlying shares & selling calls
    intrinsic_premium = Max(underlying - strike, 0);
    extrinsic_premium = total_premium - intrinsic_premium;
    return_on_capital = Max(extrinsic_premium  / underlying, 0); # clamp to [0, inf]
}

def adjusted_return_on_capital = PROFIT_TARGET * return_on_capital;

# approximate time for option to decay to X% of original value
# (model time decay as a linear process and set a floor on the hold time to prevent
# math from blowing up on weeklies)
def MIN_HOLD_TIME_DAYS = 1;
def premium_to_burn = PROFIT_TARGET * extrinsic_premium;
def days_to_decay_target = Max(MIN_HOLD_TIME_DAYS, premium_to_burn / -Theta());

def DAYS_PER_YEAR = 365;
def WEEKS_PER_YEAR = 52;
def dte = GetDaysToExpiration();
def is_weekly = dte <= 5;

def active_days_per_year;
def n_sales_per_year;

if (is_weekly) {
    # for weeklies, assume we're making at most 2 sales per week
    active_days_per_year = DAYS_PER_YEAR - (WEEKS_PER_YEAR * 2);
    n_sales_per_year = Min(2*WEEKS_PER_YEAR, TIME_EFFICIENCY * (active_days_per_year / days_to_decay_target));
} else {
    # 100% activity is baseline assumption for longer-term options
    active_days_per_year = DAYS_PER_YEAR;
    n_sales_per_year = Min(WEEKS_PER_YEAR, TIME_EFFICIENCY * (active_days_per_year / days_to_decay_target));
}

def annual_roc = Power(1 + adjusted_return_on_capital, n_sales_per_year) - 1;

AddLabel(yes, AsText(100 * annual_roc, "%1$.2f%%"), color.green);